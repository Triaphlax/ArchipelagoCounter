extends Node

signal update
signal timer_update
signal log(log_message: LogMessage)
signal load_complete
signal pre_save
signal active_player_changed(active_players: Array)
## Signal bus equivalent
signal broadcast(message: String, args: Dictionary)

var settings: Settings

var initialized := false

var items: Dictionary[String, int] = {}
var checks := 0
var total_checks := 0
var total_checks_counted_slots: Array[int] = []
var completed_games: Array[String] = []
var game_checks: Dictionary[String, int] = {}
var total_game_checks: Dictionary[String, int] = {}

var save: Save

var active_players: Array = []

var checksums := {}
var players: Array = []
var slot_data := {}
# relates game to a lookup table that relates item id to name
var item_lookup: Dictionary[String, Dictionary]
# relates game to a lookup table that relates location id to name
var location_lookup: Dictionary[String, Dictionary]
# relates slot id to game name
var game_lookup: Dictionary[int, String] = {}


func _process(delta: float) -> void:
	if len(active_players) > 0:
		save.timer += delta
		
		for player in active_players:
			var game := get_slot_from_id(player)
			if game not in save.game_timer:
				save.game_timer[game] = 0.0
			save.game_timer[game] += delta
		
		timer_update.emit()


func _ready():
	tree_exiting.connect(on_quit)
	print("Loading settings and save...")
	on_load()
	
	print("Fetching Room Info")
	var socket := Socket.new(settings.archipelago_connection_data["url"], settings.archipelago_connection_data["password"])
	add_child(socket)
	await socket.fetch_room_info()
	for slot in settings.games:
		print("Building lookup for {0} ({1})".format([slot, settings.games[slot]]))
		await build_lookup(settings.games[slot], socket)
		game_checks[slot] = 0
		total_game_checks[slot] = 0
	
	for slot in settings.games:
		var game := settings.games[slot]
		print("Fetching inventory for {0}".format([slot]))
		var inventory := await socket.fetch_inventory(game, slot, checksums[game])
		process_connected(inventory.connected_packet)
		if inventory.received_items_packet != {}:
			process_received_items(get_slot_id_from_name(slot), inventory.received_items_packet)
	
	process_save()
	
	var watch_slot: String = settings.games.keys()[0]
	var watch_game: String = settings.games[watch_slot]
	print("Watching for updates on {0} ({1})".format([watch_slot, watch_game]))
	socket.watch_for_updates(watch_game, watch_slot, checksums[watch_game])
	socket.updates_received.connect(updates_received)
	initialized = true
	update.emit()
	load_complete.emit()
	print("Load complete")


func build_lookup(game: String, socket: Socket):
	if game in item_lookup:
		return ## Game already has lookup
	
	var data_package = await get_data_package_for_game(game, socket)
	if data_package == {}:
		return
	item_lookup[game] = {} as Dictionary[int, String]
	for item in data_package["item_name_to_id"]:
		item_lookup[game][int(data_package["item_name_to_id"][item])] = item
	
	location_lookup[game] = {} as Dictionary[int, String]
	for location in data_package["location_name_to_id"]:
		location_lookup[game][int(data_package["location_name_to_id"][location])] = location
	
	checksums[game] = data_package["checksum"]


func get_data_package_for_game(game: String, socket: Socket) -> Dictionary:
	var override := settings.get_override_data_package_file_for_game(game)
	if override != {}:
		return override
	
	var data_package = await socket.fetch_data_package(game)
	if data_package == null:
		return {}
	return data_package


func process_connected(packet):
	var slot_id := int(packet["slot"])
	if slot_id in total_checks_counted_slots:
		return
	
	if players == []:
		players = packet["players"]
	
	slot_data[get_slot_from_id(slot_id)] = packet["slot_data"]
	
	total_checks_counted_slots.append(slot_id)
	
	var location_count := 0
	for location in packet["missing_locations"]:
		if not is_location_excluded(slot_id, int(location)):
			location_count += 1
	
	for location in packet["checked_locations"]:
		if not is_location_excluded(slot_id, int(location)):
			location_count += 1
	
	total_checks += location_count
	total_game_checks[get_slot_from_id(slot_id)] += location_count


func is_location_excluded(slot_id: int, location_id: int) -> bool:
	var game_name := get_game_from_slot(slot_id)
	var excluded_locations := settings.get_excluded_locations_for_game(game_name)
	
	if excluded_locations == []:
		return false
	
	var location_name := get_location_name_from_id(slot_id, location_id)
	
	for excluded_location in excluded_locations:
		var r := RegEx.create_from_string(excluded_location)
		if r.search(location_name):
			return true
	
	return false


func process_received_items(slot_id: int, packet):
	for item in packet["items"]:
		if item["player"] <= 0: #Invalid player, not real item
			continue
		
		var item_id := int(item["item"])
		var location_id := int(item["location"])
		var sending_slot_id := int(item["player"])
		
		if not is_location_excluded(sending_slot_id, location_id):
			checks += 1
			game_checks[get_slot_from_id(sending_slot_id)] += 1
		get_item(slot_id, item_id)


func updates_received(updates: Array[Socket.Update]):
	for update in updates:
		update_received(update)
		log_update(update)
	
	update.emit()


func update_received(update: Socket.Update):
	if update is Socket.Update_Player:
		var up := update as Socket.Update_Player
		if up.update_type == Socket.Update_Player.Player_Update_Type.Join:
			active_players.append(up.slot)
			active_player_changed.emit(active_players)
		elif up.update_type == Socket.Update_Player.Player_Update_Type.Part:
			active_players.erase(up.slot)
			active_player_changed.emit(active_players)
	elif update is Socket.Update_Goal:
		# Pause timer when goal is completed for a game
		active_players.erase(update.slot)
		active_player_changed.emit(active_players)
	
	if update is Socket.Update_Item:
		var ui := update as Socket.Update_Item
		
		if not is_location_excluded(ui.sending_player_id, ui.location_id):
			checks += 1
			game_checks[get_slot_from_id(ui.sending_player_id)] += 1
		get_item(ui.receiving_player_id, ui.item_id)


func get_item(slot_id: int, item_id: int):
	var slot_name := get_slot_from_id(slot_id)
	
	var item_name := get_item_name_from_id(slot_id, item_id)
	var item_code := "{0}::{1}".format([slot_name, item_name])
	if item_code not in items:
		items[item_code] = 0
	
	items[item_code] += 1


func log_update(update: Socket.Update):
	var log_object := LogMessage.from_update(update)
	if log_object == null:
		return
	
	process_log_entry(log_object)
	
	save.log.append(log_object)
	log.emit(log_object)


func get_player_name_from_id(id: int) -> String:
	for player in players:
		if int(player["slot"]) == id:
			return player["alias"]
	
	return "Someone"


func get_game_from_slot(id: int) -> String:
	if not id in game_lookup:
		var slot_name := get_slot_from_id(id)
		game_lookup[id] = settings.games[slot_name]
	
	return game_lookup[id]


func get_slot_id_from_name(name: String) -> int:
	for player in players:
		if player["name"] == name:
			return int(player["slot"])
	
	return -1


func get_slot_from_id(id: int) -> String:
	for player in players:
		if int(player["slot"]) == id:
			return player["name"]
	
	return "Someone"


func get_item_name_from_id(slot_id: int, item_id: int) -> String:
	var game := get_game_from_slot(slot_id)
	
	if item_id in item_lookup[game]:
		return item_lookup[game][item_id]
	
	return "Something"


func get_location_name_from_id(slot_id: int, location_id: int) -> String:
	var game := get_game_from_slot(slot_id)
	
	if location_id in location_lookup[game]:
		return location_lookup[game][location_id]
	
	return "Somewhere"


func get_item_count(code: String) -> int:
	if code in items:
		return items[code]
	
	return 0


func get_color_for_slot(slot: String) -> Color:
	if slot in settings.custom_slot_colors:
		return settings.custom_slot_colors[slot]
	return settings.log_default_slot_color


func on_load():
	load_settings()
	load_save()


func on_quit():
	flush_save()


func flush_save():
	pre_save.emit()
	save.save()


func load_settings():
	var settings_data := Save.load_file("APSettings.json")
	if settings_data == {}:
		return
	
	settings = Settings.new(settings_data)


func load_save():
	save = Save.load()
	timer_update.emit()


func process_save():
	for log in save.log:
		process_log_entry(log)


func process_log_entry(log_message: LogMessage):
	if log_message is LogMessage_SlotEvent and log_message.type == LogMessage_SlotEvent.TYPE.GOAL:
		completed_games.append(get_slot_from_id(log_message.slot))


func loaded():
	if initialized:
		return
	
	await load_complete
